embed-server --std-out=echo --server-config=standalone.xml

module add --name=org.postgres --resources=/tmp/postgresql-42.2.2.jar --dependencies=javax.api,javax.transaction.api

if (outcome == success) of /subsystem=datasources/jdbc-driver=postgresql:read-resource
    /subsystem=datasources/jdbc-driver=postgresql:remove
end-if
/subsystem=datasources/jdbc-driver=postgresql:add( \
    driver-name=postgresql, \
    driver-module-name=org.postgres, \
    driver-xa-datasource-class-name=org.postgresql.xa.PGXADataSource)

if (outcome == success) of /subsystem=datasources/data-source=KeycloakDS:read-resource
    /subsystem=datasources/data-source=KeycloakDS:remove
end-if
/subsystem=datasources/data-source=KeycloakDS:add( \
    jndi-name=java:jboss/datasources/KeycloakDS, \
    driver-name=postgresql, \
    connection-url="jdbc:postgresql://localhost/keycloak?currentSchema=public", \
    user-name="keycloak", password="keycloak", \
    use-java-context=true, \
    check-valid-connection-sql="select 1", \
    validate-on-match=false, \
    background-validation=true, \
    background-validation-millis=10000)

if (outcome == success) of /subsystem=undertow/server=default-server/ajp-listener=ajpListener:read-resource
    /subsystem=undertow/server=default-server/ajp-listener=ajpListener:remove
end-if

/subsystem=undertow/server=default-server/ajp-listener=ajpListener:add( \
        socket-binding=ajp, \
        enabled=true, \
        scheme=https)

stop-embedded-server